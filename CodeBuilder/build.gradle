apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
}

final bintrayUser = System.getenv('BINTRAY_USER') ?: ''
final bintrayApiKey = System.getenv('BINTRAY_API_KEY') ?: ''
final versionName = "0.3.0.${System.getenv('TRAVIS_BUILD_NUMBER') ?: 0}"

task sourceJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    publications {
        Bintray(MavenPublication) {
            groupId 'com.github.wrdlbrnft'
            artifactId 'code-builder'
            version versionName
            artifact sourceJar
            artifact new File(buildDir, '/libs/CodeBuilder.jar')

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                final configurations = ['compile', 'api', 'implementation']
                project.configurations.findAll { it.name in configurations }.each {
                    it.allDependencies.each {
                        if (it.group != null && it.name != null && it.version != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode 'groupId', it.group
                            dependencyNode.appendNode 'artifactId', it.name
                            dependencyNode.appendNode 'version', it.version
                            final dependencyArtifact = it.artifacts[0]
                            if(dependencyArtifact != null) {
                                if (dependencyArtifact.classifier != null) {
                                    dependencyNode.appendNode 'classifier', dependencyArtifact.classifier
                                }
                                if (dependencyArtifact.extension != null) {
                                    dependencyNode.appendNode 'type', dependencyArtifact.extension
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Bintray']
    pkg {
        repo = 'maven'
        name = 'CodeBuilder'
        userOrg = bintrayUser
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Wrdlbrnft/CodeBuilder'
        publicDownloadNumbers = true
        version {
            name = versionName
            released = new Date()
        }
    }
}